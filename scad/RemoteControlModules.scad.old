$fn = 100;

module round_case(xx, yy, zz, d, cw) {
    difference () {
        minkowski() {
            cube([xx - 2*d, yy - 2*d, zz], center = true);
            sphere(d/2);
        }

        // chopp off top
        translate([0, 0, zz]) cube([xx + 1, yy + 1, zz], center = true);

        // hollow inside
       translate([0,0,d]) {
           minkowski() {
               cube([xx - 2*cw - 2*d, yy - 2*cw - 2*d, zz - 2*cw + 2*d], center = true);
               sphere(d/2);
           }
       }
    }
}

module RemoteControlBottom(w, l, h, d, cw, lw, lh) {
    difference() {
        round_case(w, l, h, d, cw);
        // cut out ledge
        translate([0, 0, h/2 - lh]) {
            linear_extrude(lh + 1) {
                minkowski() {
                    square([w - 2*lw - 2*d, l - 2*lw - 2*d], center = true);
                    circle(d/2);
                }
            }
        }
    }
}

module RemoteControlTop(w, l, h, d, cw, lw, lh) {
    round_case(w, l, h, d, cw);
    // add ledge
    difference() {
        translate([0, 0, h/2 - 1]) {
            linear_extrude(lh + 1) {
                minkowski() {
                    square([w - 2*(cw - lw) - 2*d, l - 2*(cw - lw) - 2*d], center = true);
                    circle(d/2);
                }
            }
        }

        // hollow inside
        translate([0, 0, h/2 - 2]) {
            linear_extrude(lh + 3) {
                minkowski() {
                    square([w - 2*cw - 2*d, l - 2*cw - 2*d], center = true);
                    circle(d/2);
                }
            }
        }
    }
}

module BatteryPack(w = 23.5, l = 50.5, h = 9, t = 1) {
    difference() {
        cube([w, l, h], center = true);
        //cube(w - 2*t, l - 2*t, h - 2*t, center = true);
    }
}
